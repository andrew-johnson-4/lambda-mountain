
type HashtableIs<k,v> = { occupied:U64, capacity:U64, contents:Tuple<k,v>[] };
type HashtableEq<k,v> = HashtableEq { occupied: U64, capacity: U64, contents: Tuple<k,v>[] }
                       | HashtableEqEOF;

let mk-hashtable(key: Type<k>, value: Type<v>): Hashtable<k,v> = HashtableEq(0, 0, 0 as (k,v)[]);

let .lookup(table: HashtableEq<k,v>, key: k, default: v): v = (
   let capacity = (table as Tag::HashtableEq).capacity;
   if capacity > 0_u64 {
      let contents = (table as Tag::HashtableEq).contents;
      let replacing = false;
      let ki = deep-hash(key) % capacity;
      let old-kv = contents[ki];
      if mem-is-non-zero(old-kv) && old-kv.first==key { replacing = true; };
      while mem-is-non-zero(old-kv) && not(replacing) {
         ki = (ki + 1_u64) % capacity;
         old-kv = contents[ki];
         if mem-is-non-zero(old-kv) && old-kv.first==key { replacing = true; };
      };
      if replacing { default = old-kv.second; };
   };
   default
);

let .has(table: HashtableEq<k,v>, key: k): U64 = (
   let capacity = (table as Tag::HashtableEq).capacity;
   let replacing = false;
   if capacity > 0_u64 {
      let contents = (table as Tag::HashtableEq).contents;
      let ki = deep-hash(key) % capacity;
      let old-kv = contents[ki];
      if mem-is-non-zero(old-kv) && old-kv.first==key { replacing = true; };
      while mem-is-non-zero(old-kv) && not(replacing) {
         ki = (ki + 1_u64) % capacity;
         old-kv = contents[ki];
         if mem-is-non-zero(old-kv) && old-kv.first==key { replacing = true; };
      };
   };
   replacing
);

let .bind(table: HashtableEq<k,v>, key: k, value: v): HashtableEq<k,v> = (
   if is(table, (HashtableEqEOF : HashtableEq<k,v>)) {
      table = HashtableEq( 0_u64, 0_u64, (0_u64 as Tuple<k,v>[]) );
   };
   let occupied = (table as Tag::HashtableEq).occupied;
   let capacity = (table as Tag::HashtableEq).capacity;
   let contents = (table as Tag::HashtableEq).contents;
   if occupied >= capacity * 3 / 4 {
      let new-capacity = max( 1000_u64, capacity * 2_u64 );
      let new-sizeof = sizeof(Tuple<k,v>) * new-capacity;
      let new-contents = malloc(new-sizeof) as Tuple<k,v>[]; 
      memset( new-contents, 0_u32, new-sizeof );
      let old-i = 0_u64;
      while old-i < capacity {
         let old-kv = contents[old-i];
         if mem-is-non-zero(old-kv) {
             new-contents.bind-eq( new-capacity, old-kv.first, old-kv.second );
         };
         old-i = old-i + 1_u64;
      };
      contents = new-contents;
      capacity = new-capacity;
   };
   let replacing = contents.bind-eq( capacity, key, value );
   if not(replacing) { occupied = occupied + 1_u64; };
   HashtableEq( occupied, capacity, contents )
);

let .bind-eq(contents: Tuple<k,v>[], capacity: U64, key: k, value: v): U64 = (
   let replacing = false;
   let ki = deep-hash(key) % capacity;
   let old-kv = contents[ki];
   if mem-is-non-zero(old-kv) && old-kv.first==key { replacing = true; };
   while mem-is-non-zero(old-kv) && not(replacing) {
      ki = (ki + 1_u64) % capacity;
      old-kv = contents[ki];
      if mem-is-non-zero(old-kv) && old-kv.first==key { replacing = true; };
   };
   contents[ki] = Tuple ( key, value );
   replacing
);

let .as-vector(table: HashtableEq<k,v>): Vector<(k,v)> = (
   let v = mk-vector(type((k,v)));
   let capacity = (table as Tag::HashtableEq).capacity;
   if capacity != 0 {
      let old-i = 0_u64;
      while old-i < capacity {
         let old-kv = (table as Tag::HashtableEq).contents[old-i];
         if mem-is-non-zero(old-kv) then v = v.push(old-kv);
         old-i = old-i + 1;
      };
   };
   v
);

let non-zero(self: HashtableEq<k,v>): U64 = (
   match self {
      HashtableEqEOF {} => ( 0 );
      _ => ( 1 );
   }
);

let mk-hashtable-is(key: Type<k>, value: Type<v>): HashtableIs<k,v> = HashtableIs(0, 0, 0 as (k,v)[]);

let .bind(table: HashtableIs<k,v>, key: k, value: v): HashtableIs<k,v> = (
   if table.occupied >= table.capacity * 3 / 4 {
      let new-capacity = max(1000, table.capacity*2);
      let new-sizeof = sizeof((k,v)) * new-capacity;
      let new-contents = malloc(new-sizeof) as (k,v)[];
      memset(new-contents, 0, new-sizeof);
      let old-i = 0_u64;
      while old-i < table.capacity {
         let old-kv = table.contents[old-i];
         if mem-is-non-zero(old-kv) then new-contents.bind-is(new-capacity, old-kv.first, old-kv.second);
         old-i = old-i + 1;
      };
      table.contents = new-contents;
      table.capacity = new-capacity;
   };
   let replacing = table.contents.bind-is(table.capacity, key, value);
   if not(replacing) then table.occupied = table.occupied + 1;
   table
);

let .bind-is(table: (k,v)[], table-capacity: U64, key: k, value: v): U64 = (
   let replacing = false;
   let ki = hash(key) % table-capacity;
   let old-kv = table[ki];
   let old-k = old-kv.first;
   if is(old-k, key) then replacing = true;
   while mem-is-non-zero(old-kv) && not(replacing) {
      ki = (ki + 1) % table-capacity;
      old-kv = table[ki];
      old-k = old-kv.first;
      if is(old-k, key) then replacing = true;     
   };
   let kv = (key, value);
   table[ki] = kv;
   replacing
);

let .lookup(table: HashtableIs<k,v>, key: k, default: v): v = (
   if table.capacity != 0 {
      let replacing = false;
      let ki = hash(key) % table.capacity;
      let old-kv = table.contents[ki];
      let old-k = old-kv.first;
      if is(old-k,key) then replacing = true;
      while mem-is-non-zero(old-kv) && not(replacing) {
         ki = (ki + 1) % table.capacity;
         old-kv = table.contents[ki];
         old-k = old-kv.first;
         if is(old-k, key) then replacing = true;     
      };
      if replacing then default = old-kv.second;
   };
   default
);

let .as-vector(table: HashtableIs<k,v>): Vector<(k,v)> = (
   let v = mk-vector(type((k,v)));
   if table.capacity != 0 {
      let old-i = 0_u64;
      while old-i < table.capacity {
         let old-kv = table.contents[old-i];
         if mem-is-non-zero(old-kv) then v = v.push(old-kv);
         old-i = old-i + 1;
      };
   };
   v
);

let $"set[]"(table: HashtableEq<k,v>, key: k, value: v): HashtableEq<k,v> = table.bind(key, value);
let $"map::cons"(key: k, value: v, tl: HashtableEq<k,v>): HashtableEq<k,v> = tl.bind(key, value);

