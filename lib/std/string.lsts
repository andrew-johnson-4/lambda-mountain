
# TODO: instead do U8 iterator for S
# TODO: deprecate this in favor of Vector

##[[deprecated: "use vector instead"]]
let clone-rope(s: S): CString = (
   let out = mk-vector(type(U8), 64);
   out = clone-rope-impl(s, out);
   out.into(type(CString))
);

let clone-rope-impl(s: S, out: Vector<U8>): Vector<U8> = (
   match s {
      SNil {} => ();

      SCons { l=left, r=right } => (
         out = clone-rope-impl(l, out);
         out = clone-rope-impl(r, out);
      );

      SAtom { a=atom } => (
         while non-zero(a) {
            out = out.push(head-string(a));
            a = tail-string(a);
         };
      );

      r => ();
   };
   out
);

let cmp(l: CString, r: CString): Ord = (
   let c = strcmp( (l as U8[]), (r as U8[]) );
   if c < 0_i32 { LessThan() }
   else if c > 0_i32 { GreaterThan() }
   else { Equal() }
);

let print(io: IO::File, x: CString): Nil = (
   while head-string(x) != 0_u8 {
      fwrite(x as U8[], 1_u64, 1_u64, $"io::file::stdout"());
      x = tail-string(x);
   };
);

let .is-digit(base: CString): U64 = (
   if non-zero(base) {
      let r = true;
      while head-string(base) != 0_u8 && r {
         r = 48_u8 <= head-string(base) && head-string(base) <= 57_u8;
         base = tail-string(base);
      };
      r
   } else false
);

let $"[]"(base: CString, idx: U64): U8 = (
   if idx < base.length
   then (base as U8[])[idx]
   else fail("Index Out Of Bounds in String[]");
);

let .into(s: CString, tgt: Type<String>): String = intern(s);
let .into(s: CString, tgt: Type<CString>): CString = s;
let .into(s: String, tgt: Type<CString>): CString = untern(s);

let .split(s: String, sep: String): List<String> = (
   let r = [] : List<String>;
   let start-index = 0_u64;
   let current-index = 0_u64;
   while current-index < s.length {
      if s[current-index:s.length].has-prefix(sep) {
         r = cons(s[start-index:current-index],r);
         current-index = current-index + sep.length;
         start-index = current-index;
      } else {
         current-index = current-index + 1;
      };
   };
   if start-index < current-index { r = cons(s[start-index:current-index],r); };
   r.reverse;
);

let .split(s: CString, sep: CString): List<CString> = (
   let r = [] : List<CString>;
   let remainder = s;
   while non-zero(remainder) {
      if remainder.has-prefix(sep) {
         if non-zero(s) then r = cons(s.remove-suffix(remainder), r);
         remainder = remainder.remove-prefix(sep);
         s = remainder;
      } else remainder = tail-string(remainder);
   };
   if non-zero(s) then r = cons(s, r);
   r.reverse;
);

let .get-prefix(s: String, pre: String): String = (
   s[:pre.length]
);

let .get-suffix(s: String, pre: String): String = (
   s[s.length - pre.length:]
);

let head-string(x: CString): U8 = if (x as USize)==0 then 0_u8 else (x as U8[])[0_u64];
let tail-string(x: CString): CString = if (x as USize)==0 then c"" else (((x as U8[]) + 1_u64) as CString);
let head(x: CString): U8 = head-string(x);
let tail(x: CString): CString = tail-string(x);

let deep-hash(key: CString): U64 = (
   let i = 0_u64;
   let hash = 0_u64;
   while head-string(key) {
      hash = hash + head-string(key);
      key = tail-string(key);
      hash = hash + (hash << 10);
      hash = hash ^ (hash >> 6);
   };
   hash = hash + (hash << 3);
   hash = hash ^ (hash >> 11);
   hash = hash + (hash << 15);
   hash
);

let .length(s: CString): U64 = (
   let si = 0_u64;
   while (s as U8[])[si] != 0 {
      si = si + 1;
   };
   si
);

let .has-prefix(base: CString, pfx: CString): U64 = (
   let r = true;
   while head-string(pfx) {
      if head-string(base) {
         if head-string(base)==head-string(pfx) {
            base = tail-string(base);
            pfx = tail-string(pfx);
         } else {
            r = false;
            pfx = c"";
         }
      } else {
         r = false;
         pfx = c"";
      }
   };
   r
);

let .remove-prefix(base: CString, pfx: CString): CString = (
   if base.has-prefix(pfx) {
      while head-string(pfx) {
         base = tail-string(base);
         pfx = tail-string(pfx);
      };
   };
   base
);

let .has-suffix(base: CString, sfx: CString): U64 = (
   let r = false;
   while head-string(base) {
      if base==sfx then r = true;
      base = tail-string(base);
   };
   r
);

let .remove-suffix(base: CString, sfx: CString): CString = (
   let r = SNil;
   while head-string(base) {
      if base==sfx then base=c"0" # not empty string because this gets chomped
      else r = r + SAtom(clone-rope(head-string(base)));
      base = tail-string(base);
   };
   clone-rope(r)
);

let .replace(base: CString, pat: CString, n: CString): CString = (
   let r = SNil;
   while head-string(base)!=0 {
      if base.has-prefix(pat) {
         base = base.remove-prefix(pat);
         r = r + SAtom(n);
      } else {
         r = r + SAtom(clone-rope(head-string(base)));
         base = tail-string(base);
      }
   };
   clone-rope(r);
);

let .contains(base: CString, pat: CString): U64 = (
   let r = false;
   while head-string(base)!=0 {
      if base.has-prefix(pat) then r = true;
      base = tail-string(base);
   };
   r
);

let $"+"(l: CString, r: CString): CString = (
   let buf = malloc(l.length + r.length + 1) as C<"char">[];
   memset(buf, 0, l.length+r.length+1); 
   strcat(buf, l as C<"char">[]);
   strcat(buf, r as C<"char">[]);
   buf as CString
);

let print(x: CString): Nil = (
   while head-string(x) {
      putchar( head-string(x) as U32 );
      x = tail-string(x)
   }
);

let non-zero(x: CString): U64 = head-string(x) != 0;

