
let apply-and-specialize(tctx: TypeContext?, fname: CString, ft: Type, ft-denormal: Type, at: Type, blame: AST): (TypeContext?, Type) = (
   if not(can-apply(ft-denormal, at)) then exit-error("Unable to apply and specialize: \{fname}\nFunction: \{ft}\nArguments: \{at}", blame);
   let original-tctx = tctx;
   let inner-tctx = unify(ft-denormal.domain, at, blame);
   let phi-modified = mk-vector(type(CString));
   for PhiContextRow{pn=phi-id, pt=phi-tt, pblame=blame} in inner-tctx.get-or(mk-tctx()).pctx {
      tctx = tctx.bind-phi(pn, pt, pblame);
      phi-modified = phi-modified.push(pn);
   };
   (
      let move-args = phi-move-args(ft-denormal, at, blame); 
      for PhiContextRow{pn=phi-id, pt=phi-tt, pblame=blame} in move-args.get-or(mk-tctx()).pctx {
         if not(phi-modified.contains(pn))
         then tctx = tctx.bind-phi(pn, pt, pblame);
      };
   );
   if not(tctx.get-or(mk-tctx()).is-blob) && inner-tctx.has-moved then exit-error("Linear Value Used After Move In Call To \{fname} : \{ft-denormal}\nWith Arg \{at}", blame);
   if inner-tctx.is-none then exit-error("Unable to apply and specialize (unify): \{fname}\nFunction: \{ft}\nArguments: \{at}", blame);
   (tctx, let initialized-ft) = phi-initialize(tctx, ft, blame);
   let closed-type = substitute(inner-tctx, ft);
   if not(ft.is-t(c"Blob",0) || ft.is-t(c"FFI",0)) then closed-type = closed-type.phi-specialize(tctx, blame);
   if closed-type.is-open then exit-error("Unification Did Not Close Before Specialization\nfunction: \{fname} \{ft}\nargs: \{at}", blame);
   let r = substitute(inner-tctx, initialized-ft.range);
   if ft.is-t(c"Prop",0) then r = r && cons-root(at);
   if ft.is-open then {
      let special-tctx = inner-tctx.with-pctx( original-tctx.get-or(mk-tctx()).pctx );
      try-specialize(fname, ft, special-tctx, closed-type);
   };
   (tctx, r)
);

