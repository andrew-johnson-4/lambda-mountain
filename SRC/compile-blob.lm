
compile-blob := Î». (: (
   (let global-ctx (fragment-context::new()))
   (let preview-program ast-parsed-program)
   (while (non-zero preview-program) (match preview-program (
      ()
      # blob globals could provide mutable compile-time state or something?
      ( (Seq( rst (Frg( k_t rhs )) )) (
         (let fragment (fragment::new()))
         (set fragment (fragment::set( fragment 'fragment-type_s (SAtom 'Fragment_s) )))
         (set fragment (fragment::set( fragment 'fragment_s (to-s rhs) )))
         (set fragment (fragment::set-term( fragment rhs )))
         (set fragment (fragment::set-type( fragment (typeof rhs) )))
         (set global-ctx (fragment-context::bind(
            global-ctx k (typeof rhs) fragment
         )))
         (set preview-program rst)
      ))
      ( (Seq( rst (Frg( k_t (Abs( _ _ _ )) )) )) (
         (set preview-program rst)
      ))
      ( (Seq( rst (Typedef( lhs rhs )) )) (
         (set global-ctx (compile-type( global-ctx lhs rhs )))
         (set preview-program rst)
      ))
      ( (Seq( rst (Glb( k_t rhs )) )) (
         (match rhs (
            ()
            ( (Abs( _ _ _ )) (
               (let fragment (fragment::new()))
               (set fragment (fragment::set( fragment 'fragment-type_s (SAtom 'Function_s) )))
               (set fragment (fragment::set-term( fragment rhs )))
               (set fragment (fragment::set-type( fragment (typeof rhs) )))
               (set global-ctx (fragment-context::bind(
                  global-ctx k (typeof rhs) fragment
               )))
            ))
            ( _ () )
         ))
         (set preview-program rst)
      ))
      ( (Seq( rst rhs )) (
         (set preview-program rst)
      ))
   )))
   (let blob SNil)
   (set preview-program ast-parsed-program)
   (while (non-zero preview-program) (match preview-program (
      ()
      ( (Seq( rst (Glb( k_t rhs )) )) (
         (set preview-program rst)
      ))
      ( (Seq( rst (Frg( k_t rhs )) )) (
         (set preview-program rst)
      ))
      ( (Seq( rst (Typedef( lhs rhs )) )) (
         (set preview-program rst)
      ))
      ( (Seq( rst expr )) (
         (let f (blob-expr( global-ctx expr )))
         (set blob (SCons(
             (close blob)
             (close(fragment::get( f 'program_s )))
         )))
         (set preview-program rst)
      ))
   )))
   (set assemble-final (clone-rope(escape-string blob)))
   (compile-write())
) Nil);


