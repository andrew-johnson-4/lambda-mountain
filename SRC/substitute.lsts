
let substitute(ctx: AContext, v: Token): Token = (
   match ctx.lookup(v.key, mk-eof()) {
      Lit{t=token} => t;
      Var{t=token} => t;
      _ => v;
   }
);

let substitute(ctx: AContext, tt: Type): Type = (
   match tt {
      TVar{v=name} => match ctx.lookup(v, mk-eof()) {
         Lit{tv=key} => t0(tv);
         Var{tv=key} => t0(tv);
         AType{tv=tt} => tv;
         _ => tt;
      };
      TGround{tag=tag,parameters=parameters} => (
         TGround( tag, close(substitute(ctx,parameters)) )
      );
      TAnd{conjugate=conjugate} => (
         let result = mk-vector(type(Type), 0_u64);
         for c in conjugate {
            match substitute(ctx,c) {
               TAnd{rconjugate=conjugate} => for rc in rconjugate { result = result.push(rc) };
               TAny{} => ();
               rc => ( result = result.push(rc); () );
            }
         };
         if result.length==0 then ta
         else if result.length==1 then result[0]
         else tand(result)
      );
      _ => tt;
   }
);

let substitute(ctx: AContext, tt: List<Type>): List<Type> = (
   match tt {
      LCons{head=head,tail=tail} => cons( substitute(ctx,head), substitute(ctx,tail) );
      _ => tt;
   }
);

let substitute(ctx: AContext, term: AST): AST = (
   match term {
      App{left:Var{key:c"uuid"}, right:Var{x=key}} => ctx.lookup(x,term).unique;
      Var{x=key} => ctx.lookup(x,term).unique;
      Lit{} => term.unique;
      Seq{seq=seq} => (
         let ret = mk-vector(type(AST), seq.length);
         for s in seq { ret = ret.push(substitute(ctx,s)) };
         Seq(ret)
      );
      App{is-cons=is-cons,left=left,right=right} => mk-cons-or-app(is-cons,substitute(ctx,left),substitute(ctx,right));
      Abs{lhs=lhs,rhs=rhs,tt=tt} => mk-abs(substitute(ctx,lhs), substitute(ctx,rhs), tt);
      Glb{key=key,val=val} => mk-glb(key, substitute(ctx,val));
      _ => term;
   }
);

let substitute-lhs(tctx: TypeContext?, t: AST): AST = substitute-lhs(tctx, t, 0);

let substitute-lhs(tctx: TypeContext?, t: AST, idx: U64): AST = (
   match t {
      App{ ps=left, right:App{ left:Lit{key:c":",ctk=token}, right:App{ left:Var{v=key,vtk=token}, right:AType{vt=tt} } } } => (
         vt = substitute-macro(tctx, vt.accept-interface(idx)).phi-specialize(tctx,t).without-phi-keep-state;
         mk-app( substitute-lhs(tctx,ps,idx+1), 
            mk-app( mk-lit(c":",unique(ctk)), mk-app(
               mk-var(v,unique(vtk)), mk-atype(vt)
            ))
         )
      );
      App{ left:Lit{key:c":",ctk=token}, right:App{ left:Var{v=key,vtk=token}, right:AType{vt=tt} } } => (
         vt = substitute-macro(tctx, vt.accept-interface(idx)).phi-specialize(tctx,t).without-phi-keep-state;
         mk-app( mk-lit(c":",unique(ctk)), mk-app(
            mk-var(v,unique(vtk)), mk-atype(tctx.substitute(vt.accept-interface(idx)))
         ))
      );
      App{ ps=left, right:Var{v=key,vtk=token} } => mk-app(substitute-lhs(tctx,ps),mk-var(v,unique(vtk)));
      Var{v=key,vtk=token} => mk-var(v,unique(vtk));
      ASTNil{} => t;
   }
);

let substitute(tctx: TypeContext?, t: AST): AST = substitute(tctx,t,false);

let substitute(tctx: TypeContext?, t: AST, is-return: U64): AST = (
   match t {
      ASTEOF{} => t;
      ASTNil{} => t;
      App{ left:Lit{key:c":",ctk=token}, right:App{ left:Var{v=key,vtk=token}, right:AType{vt=tt} } } => (
         vt = substitute-macro(tctx, vt).phi-specialize(tctx,t,is-return);
         t = mk-app( mk-lit(c":",unique(ctk)), mk-app(mk-var(v,unique(vtk)), mk-atype(vt)) );
         let found = false;
         for TypeContextRow{tk=key,ta=def} in tctx.get-or(mk-tctx()).tctx {
            if not(found) && tk==v {
               t = mk-app( mk-lit(c":",unique(ctk)), mk-app(substitute(tctx,ta), mk-atype(vt)) );
               found = true;
            }
         }; t
      );
      App{ left:Lit{key:c":",ctk=token}, right:App{ inner-t=left, right:AType{vt=tt} } } => (
         vt = substitute-macro(tctx, vt).phi-specialize(tctx,t,is-return);
         mk-app( mk-lit(c":",unique(ctk)), mk-app(substitute(tctx, inner-t), mk-atype(vt)) );
      );
      Lit{v=key,vtk=token} => mk-lit(v,unique(vtk));
      Var{v=key,vtk=token} => mk-var(v,unique(vtk));
      Abs{lhs=lhs,rhs=rhs,tt=tt} => mk-abs(substitute-lhs(tctx,lhs),substitute(tctx,rhs,true),tctx.substitute(tt));
      AType{tt=tt} => mk-atype(tctx.substitute(tt).sanitize-phi);
      App{is-cons=is-cons,lt=left,rt=right} => mk-cons-or-app(is-cons,substitute(tctx,lt),substitute(tctx,rt));
      Seq{seq=seq} => (
         t = mk-eof();
         for s in seq { t = t + substitute(tctx,s); };
         t
      );
      Glb{k=key, rt=val} => (
         let found = false;
         for TypeContextRow{tk=key,ta=def} in tctx.get-or(mk-tctx()).tctx {
            if not(found) && k.key==tk { 
               match ta {
                  Var{tvtk=token} => k = tvtk;
                  _ => ();
               };
               found = true;
            }
         };
         mk-glb(unique(k),substitute(tctx,rt))
      );
   }
);
