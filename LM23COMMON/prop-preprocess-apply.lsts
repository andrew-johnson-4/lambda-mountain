
let preprocess-apply(program: AST): AST = preprocess-apply(program, false);

let preprocess-apply(program: AST, is-lhs: Bool): AST = (
   match program {
      App{ left:App{ left:Var{key:c"macro::concat",ctk=token}, right:App{ left:Var{lk=key}, right:Var{rk=key} } }, nt=right } => (
         mk-app( mk-var(lk+rk, ctk.unique), preprocess-apply(nt) )
      );
      App{ left:App{ left:Var{key:c"macro::concat",ctk=token}, right:App{ left:Var{lk=key}, right:Lit{rk=key} } }, nt=right } => (
         mk-app( mk-var(lk+rk, ctk.unique), preprocess-apply(nt) )
      );
      App{ left:App{ left:Var{key:c"macro::concat",ctk=token}, right:App{ left:Lit{lk=key}, right:Var{rk=key} } }, nt=right } => (
         mk-app( mk-var(lk+rk, ctk.unique), preprocess-apply(nt) )
      );
      App{ left:App{ left:Var{key:c"macro::concat",ctk=token}, right:App{ left:Lit{lk=key}, right:Lit{rk=key} } }, nt=right } => (
         mk-app( mk-var(lk+rk, ctk.unique), preprocess-apply(nt) )
      );
      App{ is-cons-1=is-cons,
         left:App{is-cons-2=is-cons,
            left:Lit{key:c":",ctk=token},
            right:App{is-cons-3=is-cons,
               t=left:Lit{l=key,ltk=token},
               right:AType{tt=tt}
            }
         }, nt=right } => (
         if not(tt.is-t(c"String",0)) then t = preprocess-apply(t);
         mk-cons-or-app(is-cons-1,
            mk-cons-or-app(is-cons-2,
               mk-lit(c":",ctk.unique),
               mk-cons-or-app(is-cons-3,t,mk-atype(tt))
            ), preprocess-apply(nt,is-lhs)
         );
      );
      App{ is-cons-1=is-cons,
         left:App{is-cons-2=is-cons,
            left:Lit{key:c":",ctk=token},
            right:App{is-cons-3=is-cons,
               t=left,
               right:AType{tt=tt}
            }
         }, nt=right } => (
         tt = phi-as-state(tt).expand-implied-phi;
         mk-cons-or-app(is-cons-1,
            mk-cons-or-app(is-cons-2,
               mk-lit(c":",ctk.unique),
               mk-cons-or-app(is-cons-3,preprocess-apply(t,is-lhs),mk-atype(tt))
            ), preprocess-apply(nt,is-lhs)
         );
      );
      App{is-cons-2=is-cons, left:Lit{key:c":",ctk=token}, right:App{is-cons-3=is-cons,t=left:Lit{},right:AType{tt=tt}}} => (
         if not(tt.is-t(c"String",0)) then t = preprocess-apply(t);
         mk-cons-or-app(is-cons-2,
            mk-lit(c":",ctk.unique),
            mk-cons-or-app(is-cons-3,t,mk-atype(tt))
         );
      );
      App{is-cons-2=is-cons, left:Lit{key:c":",ctk=token}, right:App{is-cons-3=is-cons,t=left,right:AType{tt=tt}}} => (
         tt = phi-as-state(tt).expand-implied-phi;
         t = preprocess-apply(t);
         mk-cons-or-app(is-cons-2,
            mk-lit(c":",ctk.unique),
            mk-cons-or-app(is-cons-3,t,mk-atype(tt))
         );
      );
      App{
         left:App{
            left:Var{key:c"as",atk=token},
            right:App{ t=left, right:AType{tt=tt} }
         }, nt=right
      } => (
         mk-app( mk-app( mk-var(c"as",atk.unique), mk-app(preprocess-apply(t),mk-atype(tt)) ), preprocess-apply(nt) )
      );
      Lit{l=key,ltk=token} => (
         for Tuple{sfxs=first, sfxtt=second} in parse-suffixes {
            if l.has-suffix(sfxs) {
               let lpfx = l.remove-suffix(sfxs).get-or(c"");
               program = mk-app( mk-lit(c":",with-key(ltk,c":")), mk-app( mk-lit(lpfx,with-key(ltk,lpfx)), mk-atype(sfxtt) ) )
            }
         }; program
      );
      Var{l=key,ltk=token} => (
         for Tuple{sfxs=first, sfxtt=second} in parse-suffixes {
            if l.has-suffix(sfxs) {
               let lpfx = l.remove-suffix(sfxs).get-or(c"");
               program = mk-app( mk-lit(c":",with-key(ltk,c":")), mk-app( mk-lit(lpfx,with-key(ltk,lpfx)), mk-atype(sfxtt) ) )
            }
         }; program
      );
      App{ is-cons=is-cons, left:Var{vn=key,vntk=token}, vt=right } => (
         if is-macro-head(vn,1) {
            preprocess-apply-hard(vn,1,program)
         } else {
            mk-cons-or-app(is-cons, preprocess-apply(mk-var(vn,vntk.unique)), preprocess-apply(vt))
         }
      );
      App{ is-cons-1=is-cons, left:App{ is-cons-2=is-cons, left:Var{vn=key,vntk=token}, vt0=right }, vt1=right } => (
         if is-macro-head(vn,2) {
            preprocess-apply-hard(vn,2,program)
         } else {
            mk-cons-or-app(is-cons-1,
               preprocess-apply(mk-cons-or-app(is-cons-2,
                  mk-var(vn,vntk.unique), vt0
               ),is-lhs), preprocess-apply(vt1,is-lhs)
            )
         }
      );
      Seq{seq=seq} => (
         program = mk-eof();
         for s in seq { program = program + preprocess-apply(s) };
         program
      );
      App{is-cons=is-cons, al=left, ar=right} => (
         mk-cons-or-app( is-cons, preprocess-apply(al,is-lhs), preprocess-apply(ar,is-lhs) )
      );
      Abs{al=lhs, ar=rhs, tlt=tt} => (
         if not(tlt.is-t(c"TypedMacro",0))
         then mk-abs(preprocess-apply(al,true),preprocess-apply(ar),tlt)
         else program
      );
      Glb{k=key, ar=val} => mk-glb(k.unique, preprocess-apply(ar));
      _ => program;
   }
);

