
# .without-phi preserves phi types in Arrows
# This is because phi types in arrows are not associated with the Arrow,
# they are actually part of the definition of the Arrow type
# Example:
# (A -> B) + Phi::State<A>. WILL be removed
# (A -> B + Phi::State<A>). WILL NOT be removed

let .without-phi(tt: Type): Type = (
   match tt {
      TAnd { conjugate=conjugate } => (
         let result = mk-vector(type(Type), 0_u64);
         for c in conjugate {
            c = c.without-phi;
            if non-zero(c) then result = result.push(c);
         };
         if result.length==0 then ta
         else if result.length==1 then result[0]
         else tand(result)
      );
      TGround { tag:c"Arrow", parameters:[rng..dom..] } => t2(c"Arrow",dom.without-phi-keep-state,rng.without-phi-keep-state);
      TGround { tag:c"Phi::Id", parameters:[_..] } => ta;
      TGround { tag:c"Phi::Transition", parameters:[_.._..] } => ta;
      TGround { tag:c"Phi::Initialize", parameters:[_..] } => ta;
      TGround { tag:c"Phi::State", parameters:[_..] } => ta;
      TGround { tag=tag, parameters=parameters } => ts(tag, parameters.without-phi);
      _ => tt;
   }
);

let .without-phi(tt: List<Type>): List<Type> = (
   match tt {
      [hd..tl] => cons( hd.without-phi, tl.without-phi );
      tl => tl;
   }
);
