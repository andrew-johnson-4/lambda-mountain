
let std-c-compile-type-typedef(td: AST): Nil = (
   match td { Typedef{} => (); };
   let location = (td as Tag::Typedef).location;
   let lhs-type = (td as Tag::Typedef).lhs-type;
   let implements = (td as Tag::Typedef).implements;
   let implies = (td as Tag::Typedef).implies;
   let size = (td as Tag::Typedef).size;
   let alias = (td as Tag::Typedef).alias;
   let opaque-alias = (td as Tag::Typedef).opaque-alias;
   let cases = (td as Tag::Typedef).cases;

   if cases.length > 0 {
   for concrete-type in concrete-type-instances-index.lookup(lhs-type.ground-tag-and-arity, [] : List<Type>) {
      assemble-header-section = assemble-header-section + SAtom(c"typedef struct ") + std-c-mangle-type(concrete-type, td)
                              + SAtom(c" ") + std-c-mangle-type(concrete-type, td) + SAtom(c";\n");
      let tctx = unify(lhs-type, concrete-type, td);
      continue-compile-c-typedefs-concrete = cons( (tctx, concrete-type, td), continue-compile-c-typedefs-concrete );
   }};
);

let std-c-compile-type-typedef-concrete(tctx: Maybe<TypeContext>, concrete-type: Type, td: AST): Nil = (
   match td { Typedef{} => (); _ => (fail("Invalid compile type \{concrete-type} \{td}\n"); ()); };
   let location = (td as Tag::Typedef).location;
   let implements = (td as Tag::Typedef).implements;
   let implies = (td as Tag::Typedef).implies;
   let size = (td as Tag::Typedef).size;
   let alias = (td as Tag::Typedef).alias;
   let opaque-alias = (td as Tag::Typedef).opaque-alias;
   let cases = (td as Tag::Typedef).cases;

   let is-incomplete = false;
   for Tuple{case-tag=first, case-fields=second} in cases {
      for Tuple{field-name=first, field-type=second} in case-fields {
         field-type = tctx.substitute(field-type).rewrite-type-alias;
         if is-incomplete-typedef(field-type) {
            is-incomplete = true;
         }
      }
   };
   if is-incomplete {
      continue-compile-c-typedefs-concrete = cons( (tctx, concrete-type, td), continue-compile-c-typedefs-concrete );
   } else {
      is-cstruct-hard-compiled-index = is-cstruct-hard-compiled-index.bind(concrete-type, 1_u64);

      assemble-types-section = assemble-types-section + SAtom(c"struct ") + std-c-mangle-type(concrete-type, td) + SAtom(c"{\n");
      
      let has-cases = false;
      for Tuple{case-tag=first, case-fields=second} in cases {
         if case-tag==c"" {
            for Tuple{field-name=first, field-type=second} in case-fields {
               let mangled-field-name = c"0_" + field-name;
               field-type = tctx.substitute(field-type);
               (let pre-tt, let post-tt) = std-c-mangle-declaration(field-type, td);
               assemble-types-section = assemble-types-section + SAtom(c"  ") + pre-tt + SAtom(c" ") + mangle-identifier(mangled-field-name) + post-tt + SAtom(c";\n");
            }
         } else has-cases = true;
      };

      if has-cases {
         assemble-types-section = assemble-types-section + SAtom(c"unsigned int discriminator_case_tag") + SAtom(c";\n");

         assemble-types-section = assemble-types-section + SAtom(c"  union {\n");
         let case-number = 0_u64;
         for Tuple{case-tag=first, case-fields=second} in cases {
            if case-tag!=c"" && case-fields.length > 0 {
               assemble-types-section = assemble-types-section + SAtom(c"    struct {\n");
               for Tuple{field-name=first, field-type=second} in case-fields {
                  let mangled-field-name = to-string(case-number) + c"_" + field-name;
                  field-type = tctx.substitute(field-type);
                  (let pre-tt, let post-tt) = std-c-mangle-declaration(field-type, td);
                  assemble-types-section = assemble-types-section + SAtom(c"      ") + pre-tt + SAtom(c" ") + mangle-identifier(mangled-field-name) + post-tt + SAtom(c";\n");
               };
               assemble-types-section = assemble-types-section + SAtom(c"    };\n");
            };
            case-number = case-number + 1;
         };
         assemble-types-section = assemble-types-section + SAtom(c"  };\n");
      };
      assemble-types-section = assemble-types-section + SAtom(c"};\n");
   };
);

let is-incomplete-typedef(tt: Type): U64 = is-incomplete-typedef(tt, tt.is-t(c"FlexibleArrayMember",0));
let is-incomplete-typedef(tt: Type, is-flexible-array-member: U64): U64 = (
   match tt {
      TAnd{conjugate=conjugate} => (
         let r = 0_u64;
         for c in conjugate { r = r || is-incomplete-typedef(c, is-flexible-array-member) };
         r
      );
      TAny{} => false;
      TVar{} => false;
      TGround{tag:"Array",parameters:[_..base-type..]} => if is-flexible-array-member then is-incomplete-typedef(base-type) else false;
      TGround{} => complex-type-index.lookup(tt.ground-tag-and-arity,false)
                && not(is-cstruct-hard-compiled-index.lookup(tt, false));
   }
);

let continue-compile-c-typedefs-concrete = [] : List<(TypeContext?,Type,AST)>;
let continue-compile-c-typedefs-count = 0_u64;
let is-cstruct-hard-compiled-index = {} : HashtableEq<Type,U64>;

let try-continue-compile-c-typedefs(): Nil = (
   let continue = continue-compile-c-typedefs-concrete;
   continue-compile-c-typedefs-concrete = [] : List<(TypeContext?,Type,AST)>;
   while non-zero(continue) {
      continue-compile-c-typedefs-count = continue-compile-c-typedefs-count + 1;
      if continue-compile-c-typedefs-count > 100
      then (
         eprint("Unable To Resolve Circular Type Dependency During Compilation\n");
         for Tuple{base-type=second} in continue {
            eprint("Typedef: \{base-type}\n")
         };
         exit(1);
      );
      for Tuple{tctx=first, base-type=second, body=third} in continue {
         if not(base-type.is-open) then std-c-compile-type-typedef-concrete(tctx, base-type, body);
      };
      continue = continue-compile-c-typedefs-concrete;
      continue-compile-c-typedefs-concrete = [] : List<(TypeContext?,Type,AST)>;
   }
);
