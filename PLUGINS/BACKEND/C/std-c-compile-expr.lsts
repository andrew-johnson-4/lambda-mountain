
let std-c-compile-expr(ctx: FContext, t: AST, is-stmt: Bool): Fragment = (
   let f = match t {
      Var{ key=key } => (
         let def = var-to-def(t);
         let e = std-c-fragment-context.lookup(def, mk-fragment());
         if not(non-zero(e)) {
            exit-error( untern("Unable to Find Variable Fragment in Context: \{key}"), t );
         }; e
      );
      AType{ tt=tt } => (
         if tt.is-t(c"Type") {
            mk-fragment().set(c"expression",SAtom{c"0"});
         } else {
            mk-fragment().set(c"expression",std-c-mangle-type(tt));
         }
      );
      App{ left:Abs{lhs=lhs:Var{name=key}, rhs:ASTNil{}}, rhs=right } => (
         let lt = typeof(lhs);
         let v = mk-fragment().set(c"expression",if std-c-is-ctype(lt) then SAtom{name} else SAtom{uuid()});
         let f = mk-fragment();
         std-c-fragment-context = std-c-fragment-context.bind( lhs, v );
         ctx = ctx.bind( name, lt, v );
         if lt.is-t(c"C") && lt.slot(c"C").r1.is-t(c":Label") {
            f = f.set(c"expression", v.get(c"expression") + SAtom{c":"});
         } else {
            (let pre, let post) = std-c-mangle-declaration(lt);
            f = f.set(c"frame", f.get(c"frame") + pre + SAtom{c" "}
                    + v.get(c"expression")
                    + SAtom{c" "} + post + SAtom{c";"});
         };
         match rhs {
            App{ left:Lit{key:c":"}, right:App{ left:Var{key:c"__uninitialized"}, right:AType{tt=tt} } } => ();
            _ => (
               let rf = std-c-compile-expr(ctx, rhs, false);
               f = f.set(c"frame", f.get(c"frame") + rf.get(c"frame"));
               f = f.set(c"expression", SAtom(c"(")
                 + v.get(c"expression")
                 + SAtom{c"="} + rf.get(c"expression") + SAtom{c")"});
            );
         };
         f
      );
      App{ left:Var{key:c"as"}, right:App{ inner=left, right:AType{tt=tt} } } => (
         let f = std-c-compile-expr(ctx, inner, false);
         f.set(c"expression", SAtom{c"(("} + std-c-mangle-type(tt) + SAtom{c")"} +
            f.get(c"expression") + SAtom{c")"}
         );
      );
      Lit{val=key} => (
         let tt = typeof(t);
         if tt.is-t(c"C") && tt.r1.is-t(c"uint8_t") then mk-expression(val)
         else if tt.is-t(c"C") && tt.r1.is-t(c"uint16_t") then mk-expression(val)
         else if tt.is-t(c"C") && tt.r1.is-t(c"uint32_t") then mk-expression(val)
         else if tt.is-t(c"C") && tt.r1.is-t(c"uint64_t") then mk-expression(val)
         else if tt.is-t(c"C") && tt.r1.is-t(c"int8_t") then mk-expression(val)
         else if tt.is-t(c"C") && tt.r1.is-t(c"int16_t") then mk-expression(val)
         else if tt.is-t(c"C") && tt.r1.is-t(c"int32_t") then mk-expression(val)
         else if tt.is-t(c"C") && tt.r1.is-t(c"int64_t") then mk-expression(val)
         else if tt.is-t(c"C") && tt.r1.is-t(c":Label") then mk-expression(val)
         else if tt.is-t(c"L") then mk-expression(val)
         else if tt.is-t(c"String") then mk-expression(val.escape-as-cstring)
         else if tt.is-t(c"SmartString") { 
            let guid = compile-smart-string-index.lookup(val,c"");
            if non-zero(guid) then mk-expression(guid) else {
               guid = uuid();
               compile-smart-string-index = compile-smart-string-index.bind(val,guid);
               let lit = mk-lit(val); ascript-normal(lit, t1(c"String"));
               let intern-ss = std-c-compile-call(ctx, c"intern", find-global-callable(c"intern", t1(c"String"), t), lit);
               assemble-header-section = assemble-header-section +
               mangle-c-type(typeof(t)) + SAtom{c" "} + SAtom{guid} + SAtom{c";\n"};
               assemble-string-initializer-section = assemble-string-initializer-section +
               SAtom{guid} + SAtom{c"="} + intern-ss.get(c"expression") + SAtom{c";\n"};
               mk-expression(SAtom{guid});
            }
         } else if tt.is-t(c"Regex") {
            let rgx-id = uuid();
            assemble-header-section = assemble-header-section +
            SAtom{c"regex_t "} + SAtom{rgx-id} + SAtom{c";\n"};            
            assemble-global-initializer-section = assemble-global-initializer-section +
            SAtom{c"{int return_code = regcomp(&"} + SAtom{rgx-id} + SAtom{c", "} +
            val.escape-as-cstring + SAtom{c", REG_EXTENDED); if(return_code){ fprintf(stderr,\"Could not compile regex.\"); exit(1);}};\n"};
            mk-expression(rgx-id);
         } else if tt.with-only-class.is-lm-struct {
            let tag-index = index-of-tag(val);
            mk-expression(
               SAtom{c"({"} + mangle-c-type(typeof(t)) + SAtom{c" rvalue={"} +
               SAtom{to-string(tag-index)} + SAtom{c"};rvalue;})"}
            )
         } else { match val {
            c"True" => mk-expression(c"(1)");
            c"False" => mk-expression(c"(0)");
            c"\\t" => mk-expression(c"(9)");
            c"\\n" => mk-expression(c"(10)");
            c"\\s" => mk-expression(c"(32)");
            c"\\o" => mk-expression(c"(35)");
            c"\\`" => mk-expression(c"(39)");
            c"\\[" => mk-expression(c"(40)");
            c"\\]" => mk-expression(c"(41)");
            c"\\:" => mk-expression(c"(59)");
            _ => mk-fragment().set(c"expression",SAtom{c"("} + SAtom{val} + SAtom{c")"});
         } }
      );
      App{ left:Lit{key:c":"}, right:App{ inner-term=left, right:AType{tt=tt} } } => (
         let f = std-c-compile-expr(ctx, inner-term, is-stmt);
         is-stmt = false; f;
      );
      App{ left:Var{key:c"c::compound"}, terms=right } => (
         let original-ctx = ctx;
         let e = mk-fragment();
         let terms-list = [] :: List<AST>;
         while non-zero(terms) {match terms {
            App{left=left, right=right} => (
               terms-list = cons( right, terms-list );
               terms = left;
            );
            _ => terms = ASTEOF;
         }};
         for ct in terms-list {
            let f = std-c-compile-expr(ctx, ct, true);
            ctx = open(f.context);
            e = std-c-chain( e, f, true );
         };
         e = e.set(c"expression", SAtom{c"{"} + e.get(c"frame") + e.get(c"expression") + SAtom{c"}"} );
         e = e.set(c"frame", SNil );
         ctx = original-ctx;
         e
      );
      App{ is-cons=is-cons, left=left, right=right } => (
          if not(is-cons) && typeof(left).is-arrow {
             match left {
                Var{fname=key} => std-c-compile-call(ctx, fname, left, right);
                App{ left:Var{fname=key}, right:App{ inner-term=left, right:AType{tt=tt} } } => std-c-compile-call(ctx, fname, left, inner-term);
                Lit{fname=key} => std-c-compile-constructor(ctx, fname, left, right);
                App{ left:Lit{fname=key}, right:App{ inner-term=left, right:AType{tt=tt} } } => std-c-compile-constructor(ctx, fname, left, inner-term);
                _ => (
                   print("TODO: std-c-misc-call "); std-c-print(right); print("\n"); exit(1); mk-fragment();
                );
             }
          } else {
             let f1 = std-c-compile-expr(ctx, left, true);
             ctx = open(f1.context);
             let f2 = std-c-compile-expr(ctx, right, true);
             let f3 = std-c-chain(f1, f2, is-stmt);
             is-stmt = false;
             f3
          }
      );
      ASTNil{} => (
         mk-fragment().set(c"expression",SAtom{c"({})"});
      );
      ASTEOF{} => mk-fragment();
      _ => (
         fail("TODO: std-c-compile-expr \{t}\n");
         mk-fragment();
      );
   };
   if is-stmt && non-zero(f.get(c"expression")) then f = f.set(c"expression", f.get(c"expression") + SAtom{c";"});
   f.context = close(ctx);
   f.type = typeof(t); f
);


