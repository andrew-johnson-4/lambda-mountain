
# The "LMCString" makes the CString a subtype of C<"char">[] instead of a direct alias
type alias CString suffix _s = LMCString + C<"char">[];

let .length(l: CString): USize = strlen(l) as USize;

let cmp(l: CString, r: CString): Ord = (
   let c = strcmp( l, r ) as I64;
   print("CString Str Cmp \{l}, \{r} = \{c}\n");
   if c < 0 then LessThan
   else if c > 0 then GreaterThan
   else Equal
);

let fail(msg: CString): Never = (
   fputs(msg, stderr);
   exit(1);
   () as Never
);

let fail(msg1: CString, msg2: CString): Never = (
   fputs(msg1, stderr);
   fputs(msg2, stderr);
   exit(1);
   () as Never
);

let print(msg: CString): Nil = (
   fputs(msg, stdout); ()
);

let eprint(msg: CString): Nil = (
   fputs(msg, stderr); ()
);

let $"[]"(l: CString, idx: USize): U8 = (
   (l as U8[])[idx]
);

let hash(key: CString): U64 = (
   let i = 0_sz;
   let key_length = key.length;
   let result = 0_u64;
   while i < key_length {
      result = result + key[i];
      result = result + (result << 10);
      result = result ^ (result >> 6);
      i = i + 1;
   };
   result = result + (result << 3);
   result = result ^ (result >> 11);
   result = result + (result << 15);
   result
);
