
type HashtableRowExists = HashtableRowEmpty | HashtableRowFilled | HashtableRowMoved;

type Hashtable<k,v> implies MustRetain, MustRelease
   = { data: SparseOwnedData<(HashtableRowExists,k,v)>[] };


let .release(x: Hashtable<k,v>): Nil = (
   if (x.data as USize) != 0 {
      x.data.release;
   };
   mark-as-released(x);
);

let .retain(x: Hashtable<k,v>): Hashtable<k,v> = (
   if (x.data as USize) != 0 {
      x.data.retain;
   };
   x
);

let mk-hashtable(tyk: Type<k>, tyv: Type<v>, capacity: USize): Hashtable<k,v> = (
   Hashtable( mk-sparse-owned-data(type((HashtableRowExists,k,v)), capacity) )
);

let mk-hashtable(tyk: Type<k>, tyv: Type<v>): Hashtable<k,v> = (
   # Default initial hashtable size is 0 (Stack Only)
   Hashtable( 0 as SparseOwnedData<(HashtableRowExists,k,v)>[] )
);

let mk-hashtable(capacity: USize): Hashtable<k,v> = (
   Hashtable( mk-sparse-owned-data(type((HashtableRowExists,k,v)), capacity) );
);

let mk-hashtable(): Hashtable<k,v> = (
   # Default initial hashtable size is 0 (Stack Only)
   Hashtable( 0 as SparseOwnedData<(HashtableRowExists,k,v)>[] )
);

let .realloc(h: Hashtable<k,v>, target-capacity: USize): Hashtable<k,v> = (
   let new-data = mk-sparse-owned-data(type((HashtableRowExists,k,v)), target-capacity);
   let old-i = 0_sz;
   while old-i < h.data.capacity {
      let old-kv = h.data[old-i];
      if is(old-kv.first,HashtableRowFilled)
      then new-data[old-i] = h.data[old-i];
      old-i = old-i + 1;
   };
   Hashtable(new-data)
);

let .bind(h: Hashtable<k,v>, key: k, val: v): Hashtable<k,v> = (
   if (h.data as USize) == 0 or h.data.capacity == 0 {
      # Hashtables can be allocated on the stack as empty values
      # Resize these to size 32 next
      h = mk-hashtable(32) : Hashtable<k,v>;
   };
   let occupied = h.data.occupied;
   let capacity = h.data.capacity;
   let contents = h.data;
   # Resize if >= 2/3 full
   if occupied >= capacity * 2 / 3 {
      h = h.realloc(capacity * 2);
   };
   h.bind-direct(key, val);
   h
);

let .bind-direct(h: Hashtable<k,v>, key: k, val: v): Nil = (
   if (h.data as USize) != 0 and h.data.capacity > 0 {
      let key-hash = hash(key);
      let found = false;
      let contents = h.data;
      let ki = (key-hash as USize) % contents.capacity;
      let kv = contents[ki];
      if is(kv.first,HashtableRowFilled) and kv.second==key { found = true; };
      while not(found) and is(kv.first,HashtableRowFilled) {
         ki = (ki + 1) % contents.capacity;
         kv = contents[ki];
         if is(kv.first,HashtableRowFilled) and kv.second==key { found = true; };
      };
      if not(is(kv.first,HashtableRowFilled)) then contents.occupied = contents.occupied + 1;
      contents[ki] = (HashtableRowFilled,key,val);
   };
);

let $"map::cons"(key: k, val: v, h: Hashtable<k,v>): Hashtable<k,v> = (
   h.bind(key,val);
);


let $"[]"(h: Hashtable<k,v>, key: k): Maybe<k> = (
   let row-index = h.find-row-index-by-key(key);
   if row-index == (-1 as USize)
   then (None : Maybe<v>)
   else Some(h.data[row-index].third)
);

let .find-row-index-by-key(h: Hashtable<k,v>, key: k): USize = (
   let row-index = -1 as USize;
   if (h.data as USize) != 0 and h.data.capacity > 0 {
      let key-hash = hash(key);
      let found = false;
      let contents = h.data;
      let ki = (key-hash as USize) % contents.capacity;
      let kv = contents[ki];
      if is(kv.first,HashtableRowFilled) and kv.second==key { found = true; };
      while not(is(kv.first,HashtableRowEmpty)) and not(found) {
         ki = (ki + 1) % contents.capacity;
         kv = contents[ki];
         if is(kv.first,HashtableRowFilled) and kv.second==key { found = true; };
      };
      if found then row-index = ki;
   };
   row-index
);

let .lookup(h: Hashtable<k,v>, key: k, default: v): v = h[key].get-or(default);
