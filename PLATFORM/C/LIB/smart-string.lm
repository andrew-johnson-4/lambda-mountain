
type SmartString (SmartString( data:U8[] , start:U8[] , end:U8[] , data-end:U8[] )); atom suffix SmartString _ss;

non-zero := λ(: s SmartString). (: (
   (<( (.start s) (.end s) ))
) U64);

intern := λ(: s String). (: (
   (let data-end (+( (as s U8[]) (.length s) )))
   (SmartString( (as s U8[]) (as s U8[]) data-end data-end ))
) SmartString);

intern := λ(: s SmartString). (: (
   s
) SmartString);

untern := λ(: s SmartString). (: (
   (let r SNil)
   (let s-start (.start s))
   (while (<( s-start (.end s) )) (
      (set r (+(
         r (SAtom(clone-rope([]( s-start 0_u64 ))))
      )))
      (set s-start (+( s-start 1_u64 )))
   ))
   (clone-rope r)
) String);

.length := λ(: x SmartString). (: (
   (-( (.end x) (.start x) ))
) U64);

print := λ(: x SmartString). (: (
   (let start (.start x))
   (while (!=( start (.end x) )) (
      (putchar(as ([]( start 0_u64 )) U32))
      (set start (+( start 1_u64 )))
   ))
) Nil);

[:] := λ(: x SmartString)(: low U64)(: hi U64). (: (
   ([:]( x (as low I64) (as hi I64) ))
) SmartString);
[:] := λ(: x SmartString)(: low I64)(: hi U64). (: (
   ([:]( x low (as hi I64) ))
) SmartString);
[:] := λ(: x SmartString)(: low U64)(: hi I64). (: (
   ([:]( x (as low I64) hi ))
) SmartString);

[:] := λ(: x SmartString)(: low I64)(: hi I64). (: (
   (if (<( low 0_i64 )) (
      (set low (+( (as (.length x) I64) low )))
   ) ())
   (if (==( hi minimum-I64 )) (
      (set hi (as (.length x) I64))
   ) (
      (if (<( hi 0_i64 )) (
         (set hi (+( (as (.length x) I64) hi )))
      ) ())
   ))
   (let lowp (+( (.start x) low )))
   (let hip (+( (.start x) hi )))
   (if (<( hip lowp )) (
      (print '[_s)(print low)(print ':_s)(print hi)(print ']\sof\s_s)(print(.length x))(print '\n_s)
      (fail 'Index\sOut\sOf\sBounds:\sSmartString.[:]_s)
   ) ())
   (if (<( lowp (.start x) )) (
      (print '[_s)(print low)(print ':_s)(print hi)(print ']\sof\s_s)(print(.length x))(print '\n_s)
      (fail 'Index\sOut\sOf\sBounds:\sSmartString.[:]_s)
   ) ())
   (if (>( hip (.end x) )) (
      (print '[_s)(print low)(print ':_s)(print hi)(print ']\sof\s_s)(print(.length x))(print '\n_s)
      (fail 'Index\sOut\sOf\sBounds:\sSmartString.[:]_s)
   ) ())
   (SmartString( (.data x) lowp hip (.data-end x) ))
) SmartString);

tail-string := λ(: x SmartString). (: (
   ([:]( x 1_i64 (as (.length x) I64) ))
) SmartString);

[] := λ(: x SmartString)(: low I64). (: (
   (if (<( low 0_i64 )) (
      (set low (+( (as (.length x) I64) low )))
   ) ())
   ([]( x (as low U64) ))
) U8);

[] := λ(: x SmartString)(: low U64). (: (
   (let lowp (+( (.start x) low )))
   (if (<( lowp (.start x) )) (
      (fail 'Index\sOut\sOf\sBounds:\sSmartString.[]_s)
   ) ())
   (if (>=( lowp (.end x) )) (
      (fail 'Index\sOut\sOf\sBounds:\sSmartString.[]_s)
   ) ())
   ([]( lowp 0_u64 ))
) U8);

head-string := λ(: x SmartString). (: (
   ([]( x 0_i64 ))
) U8);

.has-suffix := λ(: base SmartString)(: sfx SmartString). (: (
   (&&( (>=( (.length base) (.length sfx) ))
        (==(
          ([:]( base (as (-( (.length base) (.length sfx) )) I64) (as (.length base) I64) ))
          sfx ))
   ))
) U64);

.remove-suffix := λ(: base SmartString)(: sfx SmartString). (: (
   (if (.has-suffix( base sfx ))
       ([:]( base 0_i64 (as (-( (.length base) (.length sfx) )) I64) ))
       base)
) SmartString);

.has-prefix := λ(: base SmartString)(: pfx SmartString). (: (
   (&&( (>=( (.length base) (.length pfx) ))
      (==(
         ([:]( base 0_i64 (as (.length pfx) I64) ))
         pfx ))
   ))
) U64);

.remove-prefix := λ(: base SmartString)(: pfx SmartString). (: (
   (if (.has-prefix( base pfx ))
       ([:]( base (as (.length pfx) I64) minimum-I64 ))
       base)
) SmartString);

.replace := λ(: base SmartString)(: pat SmartString)(: n SmartString). (: (
   (let r SNil)
   (while (>( (.length base) 0_u64 )) (
      (if (.has-prefix( base pat )) (
         (set base (.remove-prefix( base pat )))
         (set r (+( r (SAtom(untern n)) )))
      ) (
         (set r (+( r (SAtom(clone-rope(head-string base))) )))
         (set base (tail-string base))
      ))
   ))
   (clone-rope r)
) String);

# Smart Strings may allocated a hidden null byte after the data to protect against any accidental CString coercions
+ := λ(: l SmartString)(: r SmartString). (: (
   (let length (+( (.length l) (.length r) )))
   # when porting this to LSTS, replace malloc with safe-alloc
   (let data (as (malloc( (+( length 1_u64 )) )) U8[]))
   (let li 0_u64)
   (while (<( li (.length l) )) (
      (set[]( data li ([]( l li )) ))
      (set li (+( li 1_u64 )))
   ))
   (let ri 0_u64)
   (while (<( ri (.length r) )) (
      (set[]( data (+( li ri )) ([]( r ri )) ))
      (set ri (+( ri 1_u64 )))
   ))
   (set[]( data length 0_u8 ))
   (SmartString( data data (+( data length )) (+( data length )) ))
) SmartString);

to-smart-string := λ(: s SmartString). (: s SmartString);
to-smart-string := λ(: s String). (: (intern s) SmartString);

deep-hash := λ(: key SmartString). (: (
   (let i 0_u64)
   (let hash 0_u64)
   (let key-start (.start key))
   (while (<( key-start (.end key) )) (
      (set hash (+( hash (as ([]( key-start 0_u64 )) U64) )))
      (set hash (+( hash (<<( hash 10_u64 )) )))
      (set hash (^( hash (>>( hash 6_u64 )) )))
      (set key-start (+( key-start 1_u64 )))
   ))
   (set hash (+( hash (<<( hash 3_u64 )) )))
   (set hash (^( hash (>>( hash 11_u64 )) )))
   (set hash (+( hash (<<( hash 15_u64 )) )))
   hash
) U64);
